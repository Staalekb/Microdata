require no.ssb.fdb:42 as db

create-dataset Enebolig

let start = 2015

let lokkeStart = 2016
let lokkeSlutt = 2024

let startAar = date_fmt($start)

let minAlder = 65
let maxAlder = 75

textblock
Første år ----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
endblock

import db/BEFOLKNING_STATUSKODE $startAar as statusStart 
keep if substr(statusStart,1,1) == '1'

import db/BEFOLKNING_KOMMNR_FORMELL $startAar as komStart
keep if inlist(substr(komStart,1,4),"1103","1141","1142")

import db/BEFOLKNING_FOEDSELS_AAR_MND as faarmnd 
generate alder = $start-int(faarmnd/100)
keep if (alder >= $minAlder & alder <= $maxAlder)

import db/BOFORHOLD_BYGNINGSTYPE_AGGR $startAar as byggningstypeStart
keep if inlist(substr(byggningstypeStart,1,2), "01","03")
generate enebolig = "Enbolig/rekkehus (2015)" 

textblock
Løkke ----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
endblock

for år in $lokkeStart : $lokkeSlutt
  let bygg_dato = $år ++ "-01-01"
  let bygg_år = Bygg ++ $år
  import db/BOFORHOLD_BYGNINGSTYPE_AGGR $bygg_dato as $bygg_år 
end

for år in $lokkeStart : $lokkeSlutt
  let status_dato = $år ++ "-01-01"
  let status_år = status ++ $år
  import  db/BEFOLKNING_STATUSKODE $status_dato as $status_år
end

for år in $lokkeStart : $lokkeSlutt
  let kom_dato = $år ++ "-01-01"
  let kom_år = kom ++ $år
  import  db/BEFOLKNING_KOMMNR_FORMELL $kom_dato as $kom_år
end

for år in $lokkeStart : $lokkeSlutt
  let statusVar = status ++ $år
  let byggVar = Bygg ++ $år
  let komVar = kom ++ $år
  let inndeling = inndeling ++ $år

  generate $inndeling = "Død" if (substr($statusVar,1,1) == '5')
  
  replace $inndeling = "Utvandret" if (substr($statusVar,1,1) == '3')
  
  replace $inndeling = "Flyttet ut av Stavanger" if \
    (substr($statusVar,1,1) == '1') & \
    (substr($komVar,1,4) != '1103' & substr($komVar,1,4) != '1141' & substr($komVar,1,4) != '1142')
  
  replace $inndeling = "Enebolig/rekkehus" if \
    inlist(substr($byggVar,1,2), "01", "03") & \
    (substr($statusVar,1,1) == '1') & \
    (inlist(substr($komVar,1,4),'1103','1141','1142'))
  
 replace $inndeling = "Leilighet/blokk" if \
    (inlist(substr($byggVar,1,2), "02", "04", "05", "06") & \
    (substr($statusVar,1,1) == '1') & \
    inlist(substr($komVar,1,4),'1103','1141','1142'))
end


textblock
Visualisering ----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
endblock

sankey enebolig \
  inndeling2020 \
  inndeling2024








  
